&НаСервере
Процедура ПриЧтенииНаСервере(ТекущийОбъект)
	ДвоичныеДанные = ТекущийОбъект.Сертификат.Получить();
	Если НЕ ДвоичныеДанные = Неопределено И ДвоичныеДанные.Размер() > 0 Тогда
		СертификатЗагружен = Истина;
	КонецЕсли; 
КонецПроцедуры

&НаСервере
Процедура ПередЗаписьюНаСервере(Отказ, ТекущийОбъект, ПараметрыЗаписи)
	Если НЕ ВременноеХранилищеФайла = "" Тогда
		ДвоичныеДанные = ПолучитьИзВременногоХранилища(ВременноеХранилищеФайла);
		Если НЕ ДвоичныеДанные = Неопределено Тогда
			ТекущийОбъект.Сертификат = Новый ХранилищеЗначения(ДвоичныеДанные, Новый СжатиеДанных(9));
		КонецЕсли; 
	КонецЕсли; 
КонецПроцедуры

&НаКлиенте
Процедура ЗагрузитьСертификат(Команда)
	НачатьПомещениеФайлаНаСервер(Новый ОписаниеОповещения("ЗагрузитьСертификатЗавершение", ЭтотОбъект),,,,, ЭтаФорма.УникальныйИдентификатор);
КонецПроцедуры

&НаКлиенте
Процедура ЗагрузитьСертификатЗавершение(ОписаниеПомещенногоФайла, ДополнительныеПараметры) Экспорт
	Если НЕ ОписаниеПомещенногоФайла.Адрес = Неопределено Тогда
		ВременноеХранилищеФайла = ОписаниеПомещенногоФайла.Адрес;
		СертификатЗагружен = Истина;
	Иначе
		СертификатЗагружен = Ложь;
	КонецЕсли; 
КонецПроцедуры




