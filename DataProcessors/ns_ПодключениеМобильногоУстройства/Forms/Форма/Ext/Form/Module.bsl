#Область ОбработчикиСобытийФормы

&НаКлиенте
Процедура ПриОткрытии(Отказ)
	СформироватьQRCode();
КонецПроцедуры

&НаКлиенте
Процедура ДляМобильнойОСПриИзменении(Элемент)
	СформироватьQRCode();
КонецПроцедуры

#КонецОбласти 

#Область СлужебныеПроцедурыИФункции

&НаКлиенте
Процедура СформироватьQRCode()
	Если ЗначениеЗаполнено(ДляМобильнойОС) Тогда
		ЗаполнитьДанныеQR(ДляМобильнойОС);
	Иначе
		ЗаполнитьДанныеQR(ПредопределенноеЗначение("Перечисление.ns_МобильныеОС.Android"));
	КонецЕсли; 
	ШК = СформироватьQRCodeСервер();
КонецПроцедуры

&НаСервере
Функция СформироватьQRCodeСервер()
	ДанныеQRКода = ДанныеQRКода(ДанныеQR, 1, 190);
	Возврат ПоместитьВоВременноеХранилище(ДанныеQRКода, УникальныйИдентификатор);
КонецФункции

&НаСервере
Функция ДанныеQRКода(QRСтрока, УровеньКоррекции, Размер)
	
	Отказ = Ложь;
	
	ГенераторQRКода = КомпонентаФормированияQRКода(Отказ);
	Если Отказ Тогда
		Возврат Неопределено;
	КонецЕсли;
	
	Попытка
		ДвоичныеДанныеКартинки = ГенераторQRКода.GenerateQRCode(QRСтрока, УровеньКоррекции, Размер);
	Исключение
		ЗаписьЖурналаРегистрации(НСтр("ru = 'Формирование QR-кода'", Метаданные.ОсновнойЯзык.КодЯзыка),
			УровеньЖурналаРегистрации.Ошибка, , , ПодробноеПредставлениеОшибки(ИнформацияОбОшибке()));
	КонецПопытки;
	
	Возврат ДвоичныеДанныеКартинки;
КонецФункции

&НаСервере
Функция КомпонентаФормированияQRКода(Отказ)
	
	СистемнаяИнформация = Новый СистемнаяИнформация;
	Платформа = СистемнаяИнформация.ТипПлатформы;
	
	ТекстОшибки = НСтр("ru = 'Не удалось подключить внешнюю компоненту для генерации QR-кода'");
	
	Попытка
		Если ПодключитьВнешнююКомпоненту("ОбщийМакет.ns_КомпонентаПечатиQRКода", "QR") Тогда
			QRCodeGenerator = Новый("AddIn.QR.QRCodeExtension");
		Иначе
			ЗаписьЖурналаРегистрации(НСтр("ru = 'Формирование QR-кода'", Метаданные.ОсновнойЯзык.КодЯзыка),
				УровеньЖурналаРегистрации.Ошибка, , , ТекстОшибки);
			Отказ = Истина;	
		КонецЕсли
	Исключение
		ЗаписьЖурналаРегистрации(НСтр("ru = 'Формирование QR-кода'", Метаданные.ОсновнойЯзык.КодЯзыка),
			УровеньЖурналаРегистрации.Ошибка, , , ПодробноеПредставлениеОшибки(ИнформацияОбОшибке()));
		Отказ = Истина;	
	КонецПопытки;
	
	Возврат QRCodeGenerator;
КонецФункции

&НаСервере
Процедура ЗаполнитьДанныеQR(МобильнаяОС)
	
	СтруктураQR = Новый Структура;
	
	Если МобильнаяОС = Перечисления.ns_МобильныеОС.Android Тогда
		СтруктураQR.Вставить("FCMServerKey", Константы.ns_FCMКлючCервера.Получить());
		СтруктураQR.Вставить("FCMSenderID", Константы.ns_FCMИдентификаторОтправителя.Получить());
		СтруктураQR.Вставить("PushServiceAdress", Константы.ns_АдресPushСервиса.Получить());
	Иначе
		СтруктураQR.Вставить("PushServiceAdress", Константы.ns_АдресPushСервиса.Получить());
	КонецЕсли; 
	
	ДанныеQR = ns_ОбщегоНазначения.ЗаписатьДанныеВJSON(СтруктураQR);
	
КонецПроцедуры

#КонецОбласти 
  


