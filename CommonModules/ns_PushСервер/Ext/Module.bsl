Функция ОтправитьPUSH(Запрос) Экспорт
	
	ТелоЗапроса = Запрос.ПолучитьТелоКакСтроку();
	
	Если ТелоЗапроса = "" Тогда
		Возврат ns_ОбщегоНазначения.ОтправитьHTTPОтветСтрокой(400, "Нет входящих данных"); 
	КонецЕсли; 
	
	Попытка
		ВходящиеДанные = ns_ОбщегоНазначения.ПрочитатьДанныеИзJSON(ТелоЗапроса);	
		Если ВходящиеДанные = Неопределено Тогда
			Возврат ns_ОбщегоНазначения.ОтправитьHTTPОтветСтрокой(400, "Ошибка преобразования данных из JSON"); 
		КонецЕсли; 
		
		Если НЕ ВозможнаОтправкаPUSH(ВходящиеДанные) Тогда
			Возврат ns_ОбщегоНазначения.ОтправитьHTTPОтветСтрокой(400, "Не хватает данных для отправки PUSH"); 	
		КонецЕсли; 
		
		Возврат PushIOS(ВходящиеДанные);	
	Исключение
	    Возврат ns_ОбщегоНазначения.ОтправитьHTTPОтветСтрокой(400, ОписаниеОшибки());
	КонецПопытки;
	
КонецФункции

Функция ВозможнаОтправкаPUSH(ВходящиеДанные)
	Если НЕ ВходящиеДанные["Text"] = Неопределено И  
		НЕ ВходящиеДанные["Data"] = Неопределено И  
		НЕ ВходящиеДанные["Recipient"] = Неопределено Тогда
			Возврат Истина;
	Иначе
		Возврат Ложь;	
	КонецЕсли; 	
КонецФункции
 
Функция PushIOS(ВходящиеДанные)
	
	Уведомление = Новый ДоставляемоеУведомление;
	Уведомление.Заголовок = ВходящиеДанные["Title"];
	Уведомление.Текст = ВходящиеДанные["Text"];
	Уведомление.Данные = ВходящиеДанные["Data"];
	Уведомление.ЗвуковоеОповещение = ЗвуковоеОповещение.ПоУмолчанию;
	Уведомление.Получатели.Добавить(ВходящиеДанные["Recipient"]);
	
	Ошибки = Новый Массив;
	
	ДанныеСертификата = Новый Соответствие;
	ДанныеСертификата.Вставить(ТипПодписчикаДоставляемыхУведомлений.APNS, РегистрыСведений.ns_APNsСертификаты.ПолучитьСертификатAPNs(Константы.ns_APNsВидСертификата.Получить()));
	
	ОтправкаДоставляемыхУведомлений.Отправить(Уведомление, ДанныеСертификата,,, Ошибки);
	
	Если Ошибки.Количество() = 0 Тогда
		Возврат ns_ОбщегоНазначения.ОтправитьHTTPОтветСтрокой(200, "PUSH успешно отправлено.");
	Иначе
		СтрОшибки = "";
		Для каждого Строка Из Ошибки Цикл
			СтрОшибки = СтрОшибки + Строка.Описание + Символы.ПС;	
		КонецЦикла; 
		Возврат ns_ОбщегоНазначения.ОтправитьHTTPОтветСтрокой(400, СтрОшибки);
	КонецЕсли; 
КонецФункции



