#Область Сериализация

Функция ЗаписатьДанныеВJSON(Данные) Экспорт
	Попытка
		ЗаписьJSON = Новый ЗаписьJSON;
		ЗаписьJSON.УстановитьСтроку(Новый ПараметрыЗаписиJSON());     
		
		НастройкиСериализацииJSON = Новый НастройкиСериализацииJSON;
		НастройкиСериализацииJSON.ВариантЗаписиДаты = ВариантЗаписиДатыJSON.ЛокальнаяДатаСоСмещением;
		НастройкиСериализацииJSON.ФорматСериализацииДаты = ФорматДатыJSON.ISO;

		ЗаписатьJSON(ЗаписьJSON, Данные, НастройкиСериализацииJSON);
		
		Возврат ЗаписьJSON.Закрыть();
	Исключение
	    ns_ОбработкаОшибок.ЗаписатьОшибку(ИнформацияОбОшибке());
		Возврат Неопределено;
	КонецПопытки; 
КонецФункции

Функция ПрочитатьДанныеИзJSON(СтрокаJSON) Экспорт
	Попытка
		ЧтениеJSON = Новый ЧтениеJSON;
		ЧтениеJSON.УстановитьСтроку(СтрокаJSON);
		Возврат ПрочитатьJSON(ЧтениеJSON, Истина,,, "ДесериализацияJSON", ns_ОбщегоНазначения);
	Исключение
		ns_ОбработкаОшибок.ЗаписатьОшибку(ИнформацияОбОшибке());
		Возврат Неопределено;
	КонецПопытки; 
КонецФункции

Функция ДесериализацияJSON(Свойство, Значение, ДополнительныеПараметры) Экспорт
	Если Свойство = "Recipient" Тогда
		Возврат ns_ОбщегоНазначения.ДесериализоватьXDTO(Значение);
	Иначе
		Возврат Значение;
	КонецЕсли; 
КонецФункции
 
Функция СериализоватьXDTO(ИсходныйОбъект) Экспорт
	
	ОбъектЗначениеXDTO = СериализаторXDTO.ЗаписатьXDTO(ИсходныйОбъект);
	ЗаписьXML = Новый ЗаписьXML;
	ЗаписьXML.УстановитьСтроку();
	ПараметрыЗаписиXML = Новый ПараметрыЗаписиXML("UTF-8", "1.0", Ложь); 
	ФабрикаXDTO.ЗаписатьXML(ЗаписьXML, ОбъектЗначениеXDTO);
	
	Возврат ЗаписьXML.Закрыть();
КонецФункции

Функция ДесериализоватьXDTO(СтрокаXML) Экспорт
	
	ЧтениеXML = Новый ЧтениеXML;
	ЧтениеXML.УстановитьСтроку(СтрокаXML);
	ИсходныйОбъект = СериализаторXDTO.ПрочитатьXML(ЧтениеXML);
	ЧтениеXML.Закрыть(); 
	
	Возврат ИсходныйОбъект;
КонецФункции

#КонецОбласти

#Область HTTPСервисы

Функция ОтправитьHTTPОтветСтрокой(КодОтвета, ТелоОтвета) Экспорт
	Ответ = Новый HTTPСервисОтвет(КодОтвета);
	Ответ.УстановитьТелоИзСтроки(ТелоОтвета, КодировкаТекста.UTF8, ИспользованиеByteOrderMark.НеИспользовать);
	Возврат Ответ;
КонецФункции
 
#КонецОбласти 